plugins {
    id 'com.github.johnrengelman.shadow'
}

allprojects {
    group = project(":chimeric-lib").maven_group
    version = project(":chimeric-lib").mod_version
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'

    base {
        archivesName = project(":chimeric-lib").archives_name + "-" + project.name + "-" + project.minecraft_compat
    }

    architectury {
        platformSetupLoomIde()
    }

    processResources {
        inputs.property 'version', project.version

        filesMatching(['fabric.mod.json', 'META-INF/neoforge.mods.toml']) {
            expand version: project.version,
                architectury_compat: project.architectury_compat,
                fabric_compat: project.fabric_compat,
                minecraft_compat: project.minecraft_compat,
                mod_id: project.mod_id,
                mod_name: project.mod_name,
                mod_description: project.mod_description,
                modmenu_compat: project.modmenu_compat,
                yacl_compat: project.yacl_compat
        }
    }

    shadowJar {
        configurations = [project.configurations.shadowBundle]
        archiveClassifier = 'dev-shadow'
    }

    remapJar {
        input.set shadowJar.archiveFile
    }

    // Configure Maven publishing.
    publishing {
        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/chimericdream/minecraft-mods"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
